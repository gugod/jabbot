#!/usr/bin/env perl
use v5.26;
use warnings;
use utf8;

use Time::Moment;
use Acme::RandomEmoji qw(random_emoji);
use Mojo::UserAgent;
use Mojo::URL;
use Encode qw(encode_utf8);
use Getopt::Long qw(GetOptions);

use FindBin;
use lib "$FindBin::Bin/../lib";

use Jabbot;
use Jabbot::Fun qw(hbars_of_top10_reservoir_usage_percentage next_full_moon_is_tonight);
use Jabbot::Remote;

sub random_emoji_message {
    my $emoji = random_emoji();
    my $url = Mojo::URL->new('https://emojipedia.org/search');
    $url->query( q => $emoji );
    return "ä»Šæ—¥è¡¨æƒ…ç¬¦è™Ÿï¼š $emoji\n\n$url";
}

sub random_reservoir_condition {
    my $hbars = hbars_of_top10_reservoir_usage_percentage();
    return "ðŸš°åå¤§æ°´åº«ç›®å‰æ°´é‡\n" . $hbars . "\nç‰¡å—çƒæ›¾éœ§æ—¥é¯‰å¾·çŸ³ç¿¡\n";
}

sub current_cns11643_char_of_the_day {
    my $base = Mojo::URL->new("https://www.cns11643.gov.tw/");
    my $res = Mojo::UserAgent->new->get($base)->result;
    my $el = $res->dom->at(".homeBanner .diary td");
    my $text = $el->all_text;

    my ($char, $bopomofo) = $text =~ m/\s(\p{Letter})\|\s+(\S+)\s/;
    my $permalink = Mojo::URL->new( $el->at("a")->attr("href") )->base($base)->to_abs;

    return sprintf(
        "ã‰† æ¯æ—¥ä¸€å­—ï¼š %s  %s\n%s",
        $char,
        $bopomofo,
        $permalink,
    );
}

sub maybe_character_of_the_day {
    my $out = "";
    eval {
        $out = current_cns11643_char_of_the_day();
        1;
    } or do {
        warn "Error: $@";
    };
    return $out;
}

sub post_to_irc {
    my $message = shift;
    my $text = $message =~ s/\n/ /gr;
    my $remote = Jabbot::Remote->new({ target => 'ircbot' });
    $remote->post({
        text    => $text,
        network => 'freenode',
        channel => '#jabbot'
    });
}

sub post_to_plurk {
    my $message = shift;
    my $remote = Jabbot::Remote->new({ target => 'plurk' });
    $remote->post({ body => $message });
}

sub post_to_twitter {
    my $message = shift;
    my $remote = Jabbot::Remote->new({ target => 'twitter' });
    $remote->post({ body => $message });
}

sub maybe_full_moon_message {
    return next_full_moon_is_tonight() ? "ä»Šæ™šæ˜¯æ»¿æœˆå‘¢ã€‚" : "";
}

sub maybe_reservoir_condition {
    return eval { random_reservoir_condition() } // "";
}

## main
my %opts;
GetOptions(
    \%opts,
    "plurk",
    "irc",
    "twitter",
);

my @messagef = (
    \&maybe_full_moon_message,
    \&maybe_reservoir_condition,
    \&random_emoji_message,
    \&maybe_character_of_the_day,
);

for my $f (@messagef) {
    my $m = $f->() or next;
    next unless $m =~ /\S/;
    say encode_utf8( $m ) . "\n--------";
    post_to_irc( $m )   if $opts{"irc"};
    post_to_plurk( $m ) if $opts{"plurk"};
    post_to_twitter( $m ) if $opts{"twitter"};
}
