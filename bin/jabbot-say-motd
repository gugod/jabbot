#!/usr/bin/env perl
use v5.26;
use strict;
use warnings;
use utf8;

use Time::Moment;
use Acme::RandomEmoji qw(random_emoji);
use Image::Randim::Source;
use Mojo::UserAgent;
use Mojo::URL;
use Net::Random;

use FindBin;
use lib "$FindBin::Bin/../lib";
use Jabbot;
use Jabbot::Util qw(time_next_full_moon);
use Jabbot::External::TaiwanReservoir;
use Jabbot::External::FlickrPublicFeed;
use Jabbot::External::NASAIotD;
use Jabbot::Remote;

sub random_emoji_message {
    my $emoji = random_emoji();
    my $url = Mojo::URL->new('https://emojipedia.org/search');
    $url->query( q => $emoji );
    return "ä»Šæ—¥è¡¨æƒ…ç¬¦è™Ÿï¼š $emoji\n\n$url";
}

sub random_nasa_image {
    my $photos = Jabbot::External::NASAIotD->photos;
    my $photo = $photos->[rand(@$photos)];
    return {
        preview    => $photo->{enclosure_url},
        source_url => $photo->{photo_page_url},
        source     => $photo->{author},
    };
}

sub random_flickr_photo {
    my $photos = Jabbot::External::FlickrPublicFeed->photos;
    my $photo = $photos->[rand(@$photos)];
    return {
        preview    => $photo->{enclosure_url},
        source_url => $photo->{photo_page_url},
        source     => "flickr: $photo->{author}",
    };
}

sub random_unsplash_image {
    my $source = Image::Randim::Source->new;
    $source->set_provider('Unsplash');
    my $image = $source->get_image;
    return {
        preview => $image->url,
        source_url => "https://unsplash.com",
        source => "Unsplash",
    }
}

sub random_image_message {
    my $image = [
        \&random_flickr_photo,
        \&random_unsplash_image,
        \&random_nasa_image,
    ]->[rand(3)]->();
    return "ä»Šæ—¥é¸åœ–ï¼š $image->{preview}\n[$image->{source}]($image->{source_url})\n";
}

sub next_full_moon_is_tonight {
    my $t_now = time;
    my $t_fullmoon = time_next_full_moon();

    my $t1 = Time::Moment->from_epoch($t_now)->at_midnight;
    my $t2 = Time::Moment->from_epoch($t_fullmoon)->at_midnight;

    return $t1 == $t2;
}

sub random_reservoir_condition {
    my $o = Jabbot::External::TaiwanReservoir->new;
    my $d = $o->usage_percentage;
    my @rows = sort { $a->{"UsagePercentage"} <=> $b->{"UsagePercentage"} } grep { $_->{"UsagePercentage"} && $_->{"ReservoirName"} =~ /æ°´åº«$/; } values %$d;
    my $picked = $rows[rand($#rows)];

    return sprintf(
        'ç¾åœ¨ %s çš„è“„æ°´é‡æ˜¯ %.2f%% å–”ã€‚',
        $picked->{"ReservoirName"},
        100*$picked->{"UsagePercentage"}
    );
}

sub post_to_plurk {
    my $messages = shift;

    my $jabbot_plurk = Jabbot::Remote->new({ target => 'plurk' });

    for my $message (@$messages) {
        $jabbot_plurk->post({ body => $message });
        sleep(2);
    }
}

sub maybe_full_moon_message {
    return next_full_moon_is_tonight() ? "ä»Šæ™šæ˜¯æ»¿æœˆå‘¢ã€‚" : "";
}

sub maybe_reservoir_condition {
    my $reservoir = eval { random_reservoir_condition() };
    if ($reservoir) {
        return "ðŸš° " . $reservoir;
    }
    return "";
}

sub maybe_today_in_history {
    my $o = eval { random_today_in_history() } // "";
    return $o;
}

sub maybe_today_birth {
    return eval { random_today_birth() } // "";
}

sub maybe_today_death {
    return eval { random_today_death() } // "";
}

sub _sections_on_wikipedia_page_today {
    state ($res, $url, $mon, $mday, %section);

    if (defined($res)) {
        return ($url, $mon, $mday, \%section);
    }

    my $tm = Time::Moment->now;
    $mon  = $tm->month;
    $mday = $tm->day_of_month;
    $url  = 'https://zh.wikipedia.org/zh-tw/' . $mon . '%E6%9C%88' . $mday . '%E6%97%A5';

    my $ua = Mojo::UserAgent->new;
    $ua->connect_timeout(15);

    $res = $ua->get($url)->result;

    unless ($res->is_success) {
        die "Failed to fetch wikipedia page: $url";
    }

    $res->dom->at("#toc")->remove;

    my $section;
    $res->dom->at("#mw-content-text")->find("h2,li")->each(
        sub {
            my ($el) = @_;
            if ($el->tag eq "h2") {
                my $title = $el->at(".mw-headline")->all_text;
                $section = $section{$title} = { items => [] };
            } else {
                push @{$section->{items}}, $el;
            }
        }
    );

    return ($url, $mon, $mday, \%section);
}

sub _extract_year_subject_link {
    my ($el) = @_;
    my ($year) = $el->all_text() =~ m/^([0-9]+)å¹´/;
    my $links = $el->find("a");
    my $subject = $links->first(sub { $_->all_text() !~ m/^([0-9]+)å¹´/; });
    return [
        $year,
        $subject->all_text(),
        "https://zh.wikipedia.org" . $subject->attr("href")
    ];
}

sub random_today_birth {
    my ($url, $mon, $mday, $section) = _sections_on_wikipedia_page_today();

    my @items = map { _extract_year_subject_link($_) } @{$section->{"å‡ºç”Ÿ"}{items}};
    my $pick = $items[rand(@items)];
    return join(
        "",
        "ðŸŽ‚ ",
        $pick->[0],
        "å¹´${mon}æœˆ${mday}æ—¥ï¼š",
        "[" . $pick->[1] . "]",
        "(" . $pick->[2] . ")",
        "å‡ºç”Ÿ"
    );
}

sub random_today_death {
    my ($url, $mon, $mday, $section) = _sections_on_wikipedia_page_today();
    my @items = map { _extract_year_subject_link($_) } @{$section->{"é€ä¸–"}{items}};
    my $pick = $items[rand(@items)];
    return join(
        "",
        "âš° ",
        $pick->[0],
        "å¹´${mon}æœˆ${mday}æ—¥ï¼š",
        "[" . $pick->[1] . "]",
        "(" . $pick->[2] . ")",
        "é€ä¸–"
    );
}

sub random_today_in_history {
    my ($url, $mon, $mday, $section) = _sections_on_wikipedia_page_today();

    my @items = grep { /\A[0-9]+å¹´ï¼š[^\n]+\z/ } map { $_->all_text } grep { not $_->at("sup.Template-Fact") } @{$section->{"å¤§äº‹è¨˜"}{items}};

    return "" unless @items;

    my $pick = $items[rand(@items)];
    $pick =~ s{\A([0-9]+å¹´)ï¼š}{$1${mon}æœˆ${mday}æ—¥ï¼š};

    return "\nðŸ•® [æ­·å²ä¸Šçš„ä»Šå¤©]($url) ï½ž $pick\n";
}

sub maybe_wikiquote_of_the_day {
    my $out = "";
    eval {
        my $url = 'https://zh.wikiquote.org/zh-tw/Wikiquote:%E6%AF%8F%E6%97%A5%E5%90%8D%E8%A8%80';
        my $ua = Mojo::UserAgent->new;
        $ua->connect_timeout(15);

        my $res = $ua->get($url)->result;
        my $el = $res->dom->at('.mw-parser-output > div:nth-child(10) > div:nth-child(1)');
        if ($el) {
            my $txt = $el->all_text =~ s/\A\s*(.+)\s*\z/$1/r;
            $out = 'â ' . $txt . " â ";
        }
    };
    return $out;
}

sub random_number_message {
    my $nr = Net::Random->new(
        src => ['random.org', 'qrng.anu.edu.au']->[rand(2)],
        min => 0,
        max => 0xFFFFFFFE,
    );

    if (my @numbers = eval { $nr->get(6) }) {
        return("ä»Šæ—¥äº‚æ•¸ï¼š" . join(' ', @numbers));
    }
    return undef;
}

## main

my @messages = grep { defined($_) && /\S/ } (
    maybe_full_moon_message(),
    maybe_reservoir_condition(),
    maybe_today_in_history(),
    maybe_today_birth(),
    maybe_today_death(),
    maybe_wikiquote_of_the_day(),
    random_emoji_message(),
    random_image_message(),
    random_number_message(),
);

say for @messages;

post_to_plurk(\@messages);
