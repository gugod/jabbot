#!/usr/bin/env perl
use v5.26;
use strict;
use warnings;
use utf8;

use Time::Moment;
use Acme::RandomEmoji qw(random_emoji);
use Image::Randim::Source;
use Mojo::UserAgent;

use FindBin;
use lib "$FindBin::Bin/../lib";
use Jabbot;
use Jabbot::Util qw(time_next_full_moon);
use Jabbot::External::TaiwanReservoir;
use Jabbot::External::FlickrPublicFeed;

use PlurkPoster;

sub random_flickr_photo {
    my $photos = Jabbot::External::FlickrPublicFeed->photos;
    my $photo = $photos->[rand(@$photos)];
    return {
        preview    => $photo->{enclosure_url},
        source_url => $photo->{photo_page_url},
        source     => "flickr: $photo->{author}",
    };
}

sub random_unsplash_image {
    my $source = Image::Randim::Source->new;
    $source->set_provider('Unsplash');
    my $image = $source->get_image;
    return {
        preview => $image->url,
        source_url => "https://unsplash.com",
        source => "Unsplash",
    }
}

sub random_image_message {
    my $image = (rand > .5) ? random_flickr_photo() : random_unsplash_image();
    return "\n\n$image->{preview} [$image->{source}]($image->{source_url})";
}

sub next_full_moon_is_tonight {
    my $t_now = time;
    my $t_fullmoon = time_next_full_moon();

    my $t1 = Time::Moment->from_epoch($t_now)->at_midnight;
    my $t2 = Time::Moment->from_epoch($t_fullmoon)->at_midnight;

    return $t1 == $t2;
}

sub random_reservoir_condition {
    my $o = Jabbot::External::TaiwanReservoir->new;
    my $d = $o->usage_percentage;
    my @rows = sort { $a->{"UsagePercentage"} <=> $b->{"UsagePercentage"} } grep { $_->{"UsagePercentage"} && $_->{"ReservoirName"} =~ /水庫$/; } values %$d;
    my $picked = $rows[rand($#rows)];

    return sprintf(
        '現在 %s 的蓄水量是 %.2f%% 喔。',
        $picked->{"ReservoirName"},
        100*$picked->{"UsagePercentage"}
    );
}

sub post_to_plurk {
    my ($message) = @_;
    my $config = Jabbot->config->{plurk};
    my $plurk = PlurkPoster->new(
        username => $config->{username},
        password => $config->{password},
    );
    $plurk->login();
    $plurk->post($message);
}

sub maybe_full_moon_message {
    return next_full_moon_is_tonight() ? "今晚是滿月呢。" : "";
}

sub maybe_reservoir_condition {
    my $reservoir = eval { random_reservoir_condition() };
    if ($reservoir) {
        return $reservoir;
    }
    return "";
}

sub maybe_today_in_history {
    my $o = eval { random_today_in_history() } // "";
    return $o;
}

sub random_today_in_history {
    my $tm = Time::Moment->now;
    my $mon = $tm->month;
    my $mday = $tm->day_of_month;

    my $url = 'https://zh.wikipedia.org/zh-tw/' . $mon . '%E6%9C%88' . $mday . '%E6%97%A5';

    my $ua = Mojo::UserAgent->new;
    my $res = $ua->get($url)->result;

    return "" unless $res->is_success;

    $res->dom->at("#toc")->remove;

    my $section;
    my %section;

    $res->dom->at("#mw-content-text")->find("h2,li")->each(
        sub {
            my ($el) = @_;
            if ($el->tag eq "h2") {
                my $title = $el->at(".mw-headline")->all_text;
                $section = $section{$title} = { items => [] };
            } else {
                push @{$section->{items}}, $el;
            }
        }
    );

    my @items = grep { /\A[0-9]+年：[^\n]+\z/ } map { $_->all_text } grep { not $_->at("sup.Template-Fact") } @{$section{"大事記"}{items}};

    return "" unless @items;

    my $pick = $items[rand(@items)];
    $pick =~ s{\A([0-9]+年)：}{$1${mon}月${mday}日：};

    return "\n🕮 [歷史上的今天]($url) ～ $pick\n";
}

## main

my $message = join(
    "",
    random_emoji(),
    maybe_full_moon_message(),
    maybe_reservoir_condition(),
    maybe_today_in_history(),
    random_image_message(),
);

post_to_plurk($message);
